plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.android'
    id 'signing'
}

android {
    namespace = "com.clevertree.md2jsx"
    compileSdk = 33

    defaultConfig {
        minSdk = 24
        targetSdk = 33
    }

    buildTypes {
        release {
            minifyEnabled = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.0"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.clevertree'
                artifactId = 'md2jsx-android'
                version = '1.0.2'

                pom {
                    name = 'MD2JSX Android'
                    description = 'High-performance Markdown to JSX AST transpiler for Android'
                    url = 'https://github.com/clevertree/md2jsx'
                    licenses {
                        license {
                            name = 'MIT OR Apache-2.0'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'clevertree'
                            name = 'Ari Asulin'
                            email = 'ari.asulin@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/clevertree/md2jsx.git'
                        developerConnection = 'scm:git:ssh://github.com/clevertree/md2jsx.git'
                        url = 'https://github.com/clevertree/md2jsx'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
            }
            mavenLocal()
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") }
        def signingKey = project.findProperty("signing.secretKey")
        def signingPassword = project.findProperty("signing.password")
        if (signingKey) {
            useInMemoryPgpKeys(signingKey, signingPassword ?: "")
        }
        sign publishing.publications.release
    }
}
